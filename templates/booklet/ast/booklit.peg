{
  package ast

  import "strings"

  func ifaceStr(val interface{}) string {
    str := ""
    for _, seg := range val.([]interface{}) {
      str = str + string(seg.([]byte))
    }

    return str
  }

  func ifaceSequences(val interface{}) []Sequence {
    seq := []Sequence{}
    for _, node := range val.([]interface{}) {
      seq = append(seq, node.(Sequence))
    }

    return seq
  }

  func ifaceNodes(val interface{}) []Node {
    nodes := []Node{}
    for _, node := range val.([]interface{}) {
      nodes = append(nodes, node.(Node))
    }

    return nodes
  }
}

Booklit <- node:Paragraphs !. {
  return node, nil
}

Paragraphs <- CommentSpacing* paragraphs:(p:Paragraph CommentSpacing* { return p, nil })+ {
  return Sequence(ifaceNodes(paragraphs)), nil
}

LineBreak <- "\n" / "\r\n"

CommentSpacing <- LineBreak / Comment

Paragraph <- lines:(l:Line LineBreak? { return l, nil })+ {
  return Paragraph(ifaceSequences(lines)), nil
}

Line <- [ \t]* words:(Word)+ {
  return Sequence(ifaceNodes(words)), nil
}

Word <- Comment? val:(String / Invoke / Interpolated) Comment? {
  return val, nil
}

Comment <- [ \t]* "{-" (Comment / !"-}" .)* "-}"

Interpolated <- '{' word:Word? '}' {
  if word == nil {
    return Sequence{}, nil
  } else {
    return word, nil
  }
}

WrappedLine <- firstWord:Word words:(Word / Split)* {
  allWords := append([]interface{}{firstWord}, words.([]interface{})...)
  return Sequence(ifaceNodes(allWords)), nil
}

Split <- LineBreak [ \t]* { return String(" "), nil }

String <- str:[^\\{}\r\n]+ { return String(c.text), nil } / '\\' [\\{}] { return String(c.text[1:]), nil }

VerbatimString <- str:[^\r\n}]+ {
  return String(c.text), nil
} / '}' !"}}" {
  return String(c.text), nil
}

PreformattedBlockLine <- indent:Indent words:Word* LineBreak {
  line := []Node{String(indent.(string))}
  line = append(line, ifaceNodes(words)...)
  return Sequence(line), nil
}

PreformattedBlock <- LineBreak lines:PreformattedBlockLine* [ \t]* {
  delete(c.globalStore, "indent-skip")
  return Preformatted(ifaceSequences(lines)), nil
}

VerbatimLine <- words:VerbatimString* {
  return Sequence(ifaceNodes(words)), nil
}

VerbatimBlockLine <- indent:Indent words:VerbatimString* LineBreak {
  line := []Node{String(indent.(string))}
  line = append(line, ifaceNodes(words)...)
  return Sequence(line), nil
}

VerbatimBlock <- LineBreak lines:VerbatimBlockLine* [ \t]* {
  delete(c.globalStore, "indent-skip")
  return Preformatted(ifaceSequences(lines)), nil
}

Invoke <- '\\' name:([a-z-]+) args:(Argument*) {
  return Invoke{
    Function: ifaceStr(name),
    Arguments: ifaceNodes(args),

    Location: Location{
      Line:   c.pos.line,
      Col:    c.pos.col,
      Offset: c.pos.offset,
    },
  }, nil
}

VerbatimArg <- "{{{" node:(VerbatimBlock / VerbatimLine) "}}}" {
  return node, nil
}

PreformattedArg <- "{{" node:PreformattedBlock "}}" {
  return node, nil
}

EmptyArg <- CommentSpacing* { return nil, nil }

Arg <- '{' node:(WrappedLine / ParaArg / EmptyArg)? '}' {
  if node == nil {
    return String(""), nil
  } else {
    return node, nil
  }
}

ParaArg <- paras:Paragraphs [ \t]* {
  return paras, nil
}

Argument <- VerbatimArg / PreformattedArg / Arg

Indent <- [ \t]* {
  skip := len(c.text)

  i, found := c.globalStore["indent-skip"]
  if found {
    skip = i.(int)
  } else {
    c.globalStore["indent-skip"] = skip
  }

  if skip <= len(c.text) {
    return string(c.text[skip:]), nil
  } else {
    return "", nil
  }
}
